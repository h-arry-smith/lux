program = _{ SOI ~ "\n"* ~ ( blockstmt ~ "\n"+)* ~ blockstmt? ~ EOI }
blockstmt = _{ top_level | stmt }

top_level = _{ delay_block }
stmt = { select | apply }

block = _{ "{" ~ "\n"+ ~ (stmt ~ "\n"+)* ~ "}" }
delay_block = { "@" ~ time ~ block }

select = { query ~ block }
query = { query_step+ } 
query_step = _{ qrange | id }
qrange = ${ id ~ ".." ~ id }
id = @{ ASCII_DIGIT+ }

apply = { ident ~ ":" ~ generator }
generator = { fade | static_value }
fade = { static_value ~ "->" ~ static_value ~ time? }
static_value = { percentage | literal }

percentage = @{ numeric ~ "%" }
literal = @{ numeric }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
numeric = @{ ("+" | "-")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
time = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ "s" }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
