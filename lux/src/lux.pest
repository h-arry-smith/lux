program = _{ SOI ~ "\n"* ~ ( blockstmt ~ "\n"+)* ~ blockstmt? ~ EOI }
blockstmt = _{ top_level | stmt }

top_level = _{ preset_block }
stmt = { select | apply | preset | delay_block | inline_delay }

block = _{ "{" ~ "\n"+ ~ (stmt ~ "\n"+)* ~ "}" }
preset_block = { "#" ~ ident ~ block }

delay_block = { "@" ~ time ~ block }
inline_delay = { "@" ~ time ~ stmt }

select = { query ~ block }
query = { query_step+ } 
query_step = _{ qrange | id | qcommand }
qrange = ${ id ~ ".." ~ id }
qcommand = ${ ":" ~ ident }
id = @{ ASCII_DIGIT+ }

apply = { param ~ ":" ~ (group | generator) }
generator = { fade | static_value }
group = { ident? ~ "{" ~ (generator ~ ",")* ~ generator? ~ "}" }
fade = { static_value ~ "->" ~ static_value ~ time? }
static_value = { percentage | literal | current_value }
current_value = { "_" }

preset = { "#" ~ ident }

percentage = @{ numeric ~ "%" }
literal = @{ numeric }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
param = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
numeric = @{ ("+" | "-")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
time = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ "s" }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
